
plugins {
    id "com.jfrog.bintray" version "1.6"
}

allprojects {
    apply plugin: "maven-publish"
    group = 'nl.weeaboo.luajpp2'
    version = '2.3.1'

    repositories {
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "https://oss.sonatype.org/content/repositories/releases/" }
    }    
}

subprojects {
    apply plugin: "eclipse"
    apply plugin: "idea"
    apply plugin: "java"

    ext {
        junitVersion = '4.12'
    }
    
    sourceCompatibility = 1.6
    targetCompatibility = 1.6
    compileJava.options.encoding = 'UTF-8'
    compileTestJava.options.encoding = 'UTF-8'

    dependencies {
        testCompile "junit:junit:$junitVersion"
    }
}

project(':core') {
    eclipse.project {
        name = "luajpp2-core"
    }
}


project(':plugin') {
    eclipse.project {
        name = "luajpp2-plugin"
    }
    
    dependencies {
        compile project(':core')
    }
}

publishing {
    publications {
        core(MavenPublication) {
            from project(":core").components.java
            artifactId = 'luajpp2-core'
        }
        plugin(MavenPublication) {
            from project(":plugin").components.java
            artifactId = 'luajpp2-plugin'
        }
    }
}

bintray {   
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['core', 'plugin']
    pkg {
        repo = 'luajpp2'
        name = 'luajpp2'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/anonl/luajpp2.git'
        version {
            name = project.version
        }
    }
}
