buildscript {
    repositories {
        google()
    }
    dependencies {
        // Plugins for Android
        classpath 'com.android.tools.build:gradle:4.1.0'
    }
}

plugins {
    // Google error-prone
    id 'net.ltgt.errorprone' version '1.1.1' apply false
}

apply from: 'dependencies.gradle'

allprojects {
    apply plugin: 'eclipse'
    apply plugin: 'idea'

    group = 'nl.weeaboo.luajpp2'
    version = '3.4.0'

    repositories {
        mavenCentral()
    }
}

subprojects {
    plugins.withType(JavaPlugin) {
        project.apply plugin: 'jacoco'

        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8

        tasks.withType(JavaCompile) {
            options.encoding = 'UTF-8'
        }

        dependencies {
            compileOnly deps.static_analysis_annotations
            implementation deps.slf4j_api

            testCompileOnly deps.static_analysis_annotations
            testImplementation deps.junit
            testRuntimeOnly deps.slf4j_desktop
        }

        jacocoTestReport {
            reports {
                xml.enabled true
            }
        }

        project.apply from: rootProject.file('errorprone.gradle')
    }
}
