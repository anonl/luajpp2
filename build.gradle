buildscript {
    repositories {
        jcenter()
        maven { url 'http://plugins.gradle.org/m2/' }
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
    }
}

apply plugin: 'com.jfrog.bintray'

allprojects {
    apply plugin: "maven-publish"
    group = 'nl.weeaboo.luajpp2'
    version = '3.0.0-beta1'

    repositories {
        jcenter()
    }    
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'jacoco'
    apply plugin: 'eclipse'
    apply plugin: 'idea'

    ext {
        javaVersion = JavaVersion.VERSION_1_6

        slf4jVersion = '1.7.21'
        junitVersion = '4.12'

        pomLicenseConfig = {
           licenses {
                license {
                    name 'MIT'
                    url 'https://opensource.org/licenses/mit-license.php'
                    distribution 'repo'
                }
            }
        }
        pomLicenseConfig.resolveStrategy = Closure.DELEGATE_ONLY // Otherwise 'license' elem is missing
    }
    
    sourceCompatibility = javaVersion
    
    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    dependencies {
        testCompile "junit:junit:$junitVersion"
    }
    
    task sourceJar(type: Jar) {
        from sourceSets.main.allJava
        classifier "sources"
    }
    
    jacocoTestReport {
        reports {
            xml.enabled true
        }
    }    
}

project(':luajpp2-core') {
    dependencies {
        compile "org.slf4j:slf4j-api:$slf4jVersion"

        testRuntime "org.slf4j:slf4j-jdk14:$slf4jVersion"
    }
    
    processResources {
        filesMatching('**/*.properties') {
            filter {
                // Engine version number
                it.replace('@VERSION@', version)
            }
        }
    }
}

project(':luajpp2-plugin') {
    dependencies {
        compile project(':luajpp2-core')
    }
}

publishing {
    publications {
        core(MavenPublication) {
            artifactId = 'luajpp2-core'

            def proj = project(":luajpp2-core")
            from proj.components.java            
            artifact(proj.sourceJar)

            pom.withXml {
                asNode().children().last() + proj.ext.pomLicenseConfig
            }  
        }
        plugin(MavenPublication) {
            artifactId = 'luajpp2-plugin'

            def proj = project(":luajpp2-plugin")
            from proj.components.java            
            artifact(proj.sourceJar)

            pom.withXml {
                asNode().children().last() + proj.ext.pomLicenseConfig
            }
        }
    }
}

bintray {   
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['core', 'plugin']
    pkg {
        repo = 'luajpp2'
        name = 'luajpp2'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/anonl/luajpp2.git'
        version {
            name = project.version
        }
    }
}
