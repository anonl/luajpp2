
plugins {
    id "com.jfrog.bintray" version "1.6"
}

allprojects {
    apply plugin: "maven-publish"
    group = 'nl.weeaboo.luajpp2'
    version = '2.4.0'

    repositories {
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/repositories/releases/" }
    }    
}

subprojects {
    apply plugin: "eclipse"
    apply plugin: "idea"
    apply plugin: "java"

    ext {
        slf4jVersion = '1.7.21'
        junitVersion = '4.12'

        pomLicenseConfig = {
           licenses {
                license {
                    name 'MIT'
                    url 'https://opensource.org/licenses/mit-license.php'
                    distribution 'repo'
                }
            }
        }
        pomLicenseConfig.resolveStrategy = Closure.DELEGATE_ONLY // Otherwise 'license' elem is missing
    }
    
    sourceCompatibility = 1.6
    targetCompatibility = 1.6
    compileJava.options.encoding = 'UTF-8'
    compileTestJava.options.encoding = 'UTF-8'

    dependencies {
        testCompile "junit:junit:$junitVersion"
    }
    
    task sourceJar(type: Jar) {
        from sourceSets.main.allJava
        classifier "sources"
    }
}

project(':core') {
    dependencies {
        compile "org.slf4j:slf4j-api:$slf4jVersion"

        testRuntime "org.slf4j:slf4j-jdk14:$slf4jVersion"
    }

    eclipse.project {
        name = "luajpp2-core"
    }
    
    processResources {
        filesMatching('**/*.properties') {
            filter {
                // Engine version number
                it.replace('@VERSION@', version)
            }
        }
    }
}

project(':plugin') {
    eclipse.project {
        name = "luajpp2-plugin"
    }
    
    dependencies {
        compile project(':core')
    }
}

publishing {
    publications {
        core(MavenPublication) {
            artifactId = 'luajpp2-core'

            def proj = project(":core")
            from proj.components.java            
            artifact(proj.sourceJar)

            pom.withXml {
                asNode().children().last() + proj.ext.pomLicenseConfig
            }  
        }
        plugin(MavenPublication) {
            artifactId = 'luajpp2-plugin'

            def proj = project(":plugin")
            from proj.components.java            
            artifact(proj.sourceJar)

            pom.withXml {
                asNode().children().last() + proj.ext.pomLicenseConfig
            }  
        }
    }
}

bintray {   
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['core', 'plugin']
    pkg {
        repo = 'luajpp2'
        name = 'luajpp2'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/anonl/luajpp2.git'
        version {
            name = project.version
        }
    }
}
